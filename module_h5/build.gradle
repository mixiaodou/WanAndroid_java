if (isDebug.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
    //引入gradle插件
    apply plugin: 'com.jakewharton.butterknife'
}

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        if (isDebug.toBoolean()) {
            applicationId "com.example.module_h5"
        }

        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 每个模块都需要 配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    //给 Module 内的资源名增加前缀, 避免资源名冲突
    //resourcePrefix "${project.name}_"
    //配置代码 目录
    sourceSets {
        main {
            if (isDebug.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    //全部Module一起编译的时候剔除debug目录
                    exclude '/debug/**'
                }
            }
        }
    }
    //使用 java 8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:'+CONSTRAINTLAYOUT
    implementation 'androidx.appcompat:appcompat:'+APPCOMPAT

    //
    //agentweb WebView库 https://github.com/Justson/AgentWeb
    api 'com.just.agentweb:agentweb-androidx:4.1.4' // (必选)
    api 'com.just.agentweb:filechooser-androidx:4.1.4'// (可选)
    api 'com.download.library:downloader-androidx:4.1.4'// (可选)
    implementation project(path: ':lib_common')
    //ARouter 每个模块都需要 配置
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
    //ButterKnife 每个模块都需要 配置
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'
}
